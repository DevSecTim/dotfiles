# Workspace
if [[ -d "$HOME/Workspace" ]]; then
    export WORKSPACE="$HOME/Workspace"
elif [[ -d "$HOME/workspace" ]]; then
    export WORKSPACE="$HOME/workspace"
fi
export CDPATH="$WORKSPACE:$WORKSPACE/github.com:$WORKSPACE/gitlab.com"
export PATH="$HOME/.local/bin${WORKSPACE:+:$WORKSPACE/_local/scripts}:/usr/local/bin:$PATH"
export CPATH="$HOME/.local/include:$CPATH"
export LIBRARY_PATH="$HOME/.local/lib:$LIBRARY_PATH"

# macOS specific
if [[ "$OSTYPE" == "darwin"* ]]; then
    [[ -f "$HOME/.iterm2_shell_integration.zsh" ]] && source "$HOME/.iterm2_shell_integration.zsh"
    [[ -d /opt/homebrew ]] && export HOMEBREW_PREFIX=/opt/homebrew

# Linux specific
elif [[ "$OSTYPE" == "linux-gnu" ]]; then
    export WINEARCH=win32
    export WINEPREFIX=~/.wine/win32
    [[ -d /home/linuxbrew/.linuxbrew ]] && export HOMEBREW_PREFIX=/home/linuxbrew/.linuxbrew
fi

# Homebrew — hardcoded prefix avoids slow `brew --prefix` subprocess call
if [[ -n "$HOMEBREW_PREFIX" ]]; then
    export PATH="$HOMEBREW_PREFIX/bin:$PATH"
    export CPATH="$HOMEBREW_PREFIX/include:$CPATH"
    export LIBRARY_PATH="$HOMEBREW_PREFIX/lib:$LIBRARY_PATH"
    export LD_LIBRARY_PATH="$HOMEBREW_PREFIX/lib:$LD_LIBRARY_PATH"
    export HOMEBREW_NO_ENV_HINTS=1
fi

# Oh My Zsh
if [[ -d "$HOME/.oh-my-zsh" ]]; then
    ZSH_THEME="half-life"
    plugins=(git dotenv)
    export ZSH="$HOME/.oh-my-zsh"
    source "$ZSH/oh-my-zsh.sh"
fi

# Cached completion helper
#  — regenerates only when the binary is newer than the cache
_cached_completion() {
    local cmd=$1; shift
    local cache="${XDG_CACHE_HOME:-$HOME/.cache}/zsh/${cmd}.zsh"
    if [[ ! -f "$cache" || "$(command -v $cmd)" -nt "$cache" ]]; then
        mkdir -p "${cache:h}"
        "$@" > "$cache" 2>/dev/null || { rm -f "$cache"; return 1; }
    fi
    source "$cache"
}

# Vim
if (( $+commands[vim] )); then
    export VISUAL='vim'
    export EDITOR=$VISUAL
fi

# Python (Pyenv)
if [[ -d "$HOME/.pyenv" ]]; then
    export PYENV_ROOT="$HOME/.pyenv"
    export PATH="$PYENV_ROOT/bin:$PYENV_ROOT/shims:$PATH"
    eval "$(command pyenv init -)"
    eval "$(command pyenv virtualenv-init -)"
fi

# Golang
if [[ -d /usr/local/go ]]; then
    export GOPATH="$HOME/.local/go"
    export PATH="$GOPATH/bin:/usr/local/go/bin:$PATH"
fi

# Terraform
if (( $+commands[terraform] )); then
    complete -o nospace -C "${commands[terraform]}" terraform
fi

# Kubernetes
if (( $+commands[kubectl] )); then
    _CTX='echo "Kubernetes Context: $(kubectl config current-context)";'$_CTX
    (( $+commands[stern] ))  && _cached_completion stern  stern --completion=zsh
    (( $+commands[argocd] )) && _cached_completion argocd argocd completion zsh

    if (( $+commands[kubectx] )); then
        alias kx='kubectx'
        RPROMPT=$RPROMPT'%{$(echotc UP 1)%} $(kubectx_prompt_info)%{$(echotc DO 1)%}'
        for ctx in $(kx | grep -i prod); do
            kubectx_mapping[$ctx]="%{$bg[red]%}%{$fg[yellow]%} $ctx PROD! %{$reset_color%}"
        done
    fi
fi

# AWS
if (( $+commands[aws] )); then
    _CTX='echo AWS Account: $(aws iam list-account-aliases --output json | jq -r ".AccountAliases[0]");'$_CTX
    (( $+commands[aws_completer] )) && complete -C aws_completer aws
    (( $+commands[fzf] )) && alias awsp='export AWS_PROFILE=$(sed -n "s/\[profile \(.*\)\]/\1/gp" ~/.aws/config | fzf)'
fi

# Azure
if (( $+commands[az] )); then
    _CTX='echo "Azure Subscription: $(az account show | jq -r .name)";'$_CTX
fi

# GCP
if (( $+commands[gcloud] )); then
    _CTX='echo "GCP Project: $(gcloud config get-value project)";'$_CTX
fi

# Rancher Desktop
[[ -d "$HOME/.rd" ]] && export PATH="$HOME/.rd/bin:$PATH"

# Aliases and functions
alias ctx='eval $_CTX'
mkcd() { mkdir -p "$1" && cd "$1" }
alias ..='cd ..'

# Source local files
if [[ -d "$HOME/.local/zshrc.d" ]]; then
    for file in "$HOME/.local/zshrc.d/"*(N); do
        source "$file"
    done
fi
